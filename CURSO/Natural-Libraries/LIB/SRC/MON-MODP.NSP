* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/** Modificación de <b>Monedas</b>.
DEFINE DATA LOCAL
  1 VISTA-MONEDA VIEW OF MONEDAS
    2 COD-MONEDA
    2 DESC
*
  1 #COD-MONEDA (A03)
  1 #DESC       (A20)
  1 #CONF       (A01)
  1 #CONF-ACT   (L)
END-DEFINE
*
DEFINE WINDOW AVISO
  SIZE 7  * 50
  BASE 10 / 30
  FRAMED
*
/*-------------------- MAIN -----------------------------------------*/
SET KEY ALL
REPEAT
  INPUT 'INGRESE CÓDIGO DE MONEDA A MODIFICAR: ' #COD-MONEDA

  DECIDE ON FIRST VALUE *PF-KEY
    VALUE 'ENTR'
      IF #COD-MONEDA EQ ' '
        REINPUT 'DEBE INGRESAR UN CÓDIGO DE MONEDA'
      END-IF
*
      PERFORM MODIFICACION-MONEDA
      INPUT USING MAP 'MON-MODM'
      RESET #COD-MONEDA #DESC
      ESCAPE BOTTOM   
*
    VALUE 'PF3'
      ESCAPE BOTTOM
*
    NONE VALUE
      IGNORE
  END-DECIDE
END-REPEAT
*
FETCH 'MON-MENP'
/*-------------------- SUBRUTINAS -----------------------------------*/
*********************************************
* SUBRUTINA MODIFICACION-MONEDA
********************************************
DEFINE SUBROUTINE MODIFICACION-MONEDA
*
F1. FIND VISTA-MONEDA WITH VISTA-MONEDA.COD-MONEDA = #COD-MONEDA
*
    IF NO RECORD FOUND
      SET WINDOW 'AVISO'
      INPUT 'LA MONEDA A ELIMINAR NO EXISTE'
      SET WINDOW OFF
      ESCAPE BOTTOM
    END-NOREC
*
    MOVE VISTA-MONEDA.COD-MONEDA TO #COD-MONEDA
    MOVE VISTA-MONEDA.DESC       TO #DESC
*    
    PERFORM MOSTRAR-PANTALLA-MONEDA
    PERFORM CONFIRMAR-ACT-MONEDA
*
    IF #CONF-ACT EQ TRUE
      UPDATE (F1.)
      END TRANSACTION
      SET WINDOW 'AVISO'
      INPUT 'LA MONEDA HA SIDO MODIFICADA'
      SET WINDOW OFF
      ESCAPE BOTTOM
    END-IF
*
  END-FIND
END-SUBROUTINE
*********************************************
* SUBRUTINA MOSTRAR-PANTALLA-MONEDA
*********************************************
DEFINE SUBROUTINE MOSTRAR-PANTALLA-MONEDA
  INPUT
    'CÓDIGO DE MONEDA:' #COD-MONEDA (AD=O) /
    'DESCRIPCIÓN.....:' #DESC       (AD=M) /
END-SUBROUTINE
*
*********************************************
* SUBRUTINA CONFIRMAR-ACT-MONEDA
*********************************************
DEFINE SUBROUTINE CONFIRMAR-ACT-MONEDA         /* EX: CONFIRMAR
  SET WINDOW 'AVISO'
  INPUT
    / '¿DESEA CONFIRMAR LA ACTUALIZACIÓN? (S/N): ' #CONF
  SET WINDOW OFF
*
  IF #CONF = 'S' OR #CONF = 's'
     MOVE #COD-MONEDA TO VISTA-MONEDA.COD-MONEDA
     MOVE #DESC       TO VISTA-MONEDA.DESC
     MOVE TRUE        TO #CONF-ACT                /* VARIABLE LÓGICA
  END-IF
END-SUBROUTINE
*
END
