* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/** Baja de <b>Departamentos</b>.
DEFINE DATA LOCAL
  1 VISTA-DEPT VIEW OF DEPARTAMENTOS
    2 COD-DEPT
    2 DESC
*
  1 #COD-DEPT   (A06)
  1 #ISN        (P10)
  1 #CONF       (A01)
END-DEFINE
*
DEFINE WINDOW AVISO
  SIZE 7  * 50
  BASE 10 / 30
  FRAMED
*
/*-------------------- MAIN -----------------------------------------*/
*
SET KEY ALL
REPEAT
  INPUT USING MAP 'DEP-BAJM'
  DECIDE ON FIRST VALUE *PF-KEY
*
    VALUE 'ENTR'                   /* ENTR = CONFIRMAR INGRESO DE DATO           
      PERFORM INGRESAR-CODIGO-DEPARTAMENTO
      PERFORM BUSCAR-DEPARTAMENTO
*
      DECIDE ON FIRST VALUE #CONF
        VALUE 'S'                  /* S    = CONFIRMAR LA BAJA DEL DEPT
          GE. GET VISTA-DEPT #ISN
          DELETE (GE.)
          END TRANSACTION
          RESET   #COD-DEPT #CONF
*
        VALUE 'N'                  /* N    = CANCELAR LA BAJA DEL DEPT
          INPUT 'SE CANCELÓ LA ELIMINACIÓN DEL REGISTRO'
          RESET #COD-DEPT #CONF
*
        NONE VALUE
          REINPUT 'INGRESE UNA OPCIÓN VÁLIDA'
      END-DECIDE
*
    VALUE 'PF3'                    /* PF3  = SALIR DEL PROGRAMA
      ESCAPE BOTTOM
*
    NONE VALUE
      IGNORE
  END-DECIDE
END-REPEAT
*
FETCH 'DEP-MENP'
*
/*-------------------- SUBRUTINAS -----------------------------------*/
*
*********************************************
* SUBRUTINA INGRESAR-CODIGO-DEPARTAMENTO
*********************************************
*
DEFINE SUBROUTINE INGRESAR-CODIGO-DEPARTAMENTO
  IF #COD-DEPT EQ ' '
    REINPUT 'DEBE INGRESAR UN CODIGO DE MONEDA'
  END-IF
END-SUBROUTINE
*
*********************************************
* SUBRUTINA BUSCAR-DEPARTAMENTO
*********************************************
*
DEFINE SUBROUTINE BUSCAR-DEPARTAMENTO
*
F1. FIND VISTA-DEPT WITH VISTA-DEPT.COD-DEPT = #COD-DEPT
*
  IF NO RECORD FOUND
    REINPUT 'EL REGISTRO NO EXISTE - INGRESE CODIGO DE MONEDA'
  END-NOREC
*
  #ISN := *ISN(F1.)
*
  SET WINDOW 'AVISO'
  INPUT 'CÓDIGO DE MONEDA: ' VISTA-DEPT.COD-DEPT (AD=O) /
        'DESCRIPCIÓN.....: ' VISTA-DEPT.DESC     (AD=O) /
        '¿DESEA ELIMINAR ESTE REGISTRO? (S/N): ' #CONF
  SET WINDOW OFF
*
END-FIND
*
END-SUBROUTINE
*
END

