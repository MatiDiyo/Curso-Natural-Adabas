* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/** Modificación de <b>Departamentos</b>.
DEFINE DATA LOCAL
  1 VISTA-DEPT VIEW OF DEPARTAMENTOS
    2 COD-DEPT
    2 DESC
*
  1 #COD-DEPT   (A03)
  1 #DESC       (A30)
  1 #CONF       (A01)
  1 #CONF-ACT   (L)
END-DEFINE
*
DEFINE WINDOW AVISO
  SIZE 7  * 50
  BASE 10 / 30
  FRAMED
*
/*-------------------- MAIN -----------------------------------------*/
SET KEY ALL
REPEAT
  INPUT 'INGRESE CÓDIGO DE DEPARTAMENTO A MODIFICAR: ' #COD-DEPT

  DECIDE ON FIRST VALUE *PF-KEY
    VALUE 'ENTR'
      IF #COD-DEPT EQ ' '
        REINPUT 'DEBE INGRESAR UN CÓDIGO DE MONEDA'
      END-IF
*
      PERFORM MODIFICACION-DEPARTAMENTO
      RESET #COD-DEPT
*      INPUT USING MAP 'DEP-MODM'    
*
    VALUE 'PF3'
      ESCAPE BOTTOM
*
    NONE VALUE
      IGNORE
  END-DECIDE
END-REPEAT
*
FETCH 'DEP-MENP'
/*-------------------- SUBRUTINAS -----------------------------------*/
*********************************************
* SUBRUTINA MODIFICACION-DEPARTAMENTO
********************************************
DEFINE SUBROUTINE MODIFICACION-DEPARTAMENTO
*
F1. FIND VISTA-DEPT WITH VISTA-DEPT.COD-DEPT = #COD-DEPT
*
    IF NO RECORD FOUND
      SET WINDOW 'AVISO'
      INPUT 'EL DEPARTAMENTO A ELIMINAR NO EXISTE'
      SET WINDOW OFF
      ESCAPE BOTTOM
    END-NOREC
*
    MOVE VISTA-DEPT.COD-DEPT TO #COD-DEPT
    MOVE VISTA-DEPT.DESC     TO #DESC
*    
    PERFORM MOSTRAR-PANTALLA-DEPARTAMENTO
    PERFORM CONFIRMAR-ACT-DEPARTAMENTO
*
    IF #CONF-ACT EQ TRUE
      UPDATE (F1.)
      END TRANSACTION
      SET WINDOW 'AVISO'
      INPUT 'EL DEPARTAMENTO HA SIDO MODIFICADA'
      SET WINDOW OFF
      ESCAPE BOTTOM
    END-IF
*
  END-FIND
END-SUBROUTINE
*********************************************
* SUBRUTINA MOSTRAR-PANTALLA-DEPARTAMENTO
*********************************************
DEFINE SUBROUTINE MOSTRAR-PANTALLA-DEPARTAMENTO
  INPUT
    'CÓDIGO DE MONEDA:' #COD-DEPT (AD=O) /
    'DESCRIPCIÓN.....:' #DESC     (AD=M) /
END-SUBROUTINE
*
*********************************************
* SUBRUTINA CONFIRMAR-ACT-DEPARTAMENTO
*********************************************
DEFINE SUBROUTINE CONFIRMAR-ACT-DEPARTAMENTO         /* EX: CONFIRMAR
  SET WINDOW 'AVISO'
  INPUT
    / '¿DESEA CONFIRMAR LA ACTUALIZACIÓN? (S/N): ' #CONF
  SET WINDOW OFF
*
  IF #CONF = 'S' OR #CONF = 's'
     MOVE #COD-DEPT TO VISTA-DEPT.COD-DEPT
     MOVE #DESC     TO VISTA-DEPT.DESC
     MOVE TRUE      TO #CONF-ACT                /* VARIABLE LÓGICA
  END-IF
END-SUBROUTINE
*
END
  
