* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
DEFINE DATA LOCAL
1 EMPL-VISTA VIEW OF EMPLEADOS
    2 ID-EMPLEADO                  (A08)   /* DESCRIPTOR
    2 NOMBRE-COMPLETO
        3 NOMBRE                   (A20)
        3 APELLIDO                 (A20)   /* DESCRIPTOR
        3 NOMBRE-SECUNDARIO        (A20)
*        
    2 ESTADO-CIVIL                 (A01)
    /* M=MARRIED  (CASADO/A)
    /* S=SINGLE   (SOLTERO/A)
    /* D=DIVORCED (DIVORCIADO/A)
    /* W=WIDOWED  (VIUDO/A)
*    
    2 SEXO                         (A01)
    /* M=MALE     (MASCULINO)
    /* F=FEMALE   (FEMENINO)
*    
    2 DOMICILIO-COMPLETO
        3 DOMICILIO(1:1)
        3 CIUDAD                   (A20)   /* DESCRIPTOR
        3 CODIGO-POSTAL            (A10)
        3 INICIAL-PAIS             (A03)
    2 TELEFONO
        3 CODIGO-AREA              (A06)
        3 NUMERO-TELEFONO          (A15)
    2 DEPARTAMENTO                 (A06)   /* DESCRIPTOR
    2 PROFESION                    (A25)   /* DESCRIPTOR
    2 INGRESO-ANUAL
        3 CODIGO-MONEDA(1:1)
        3 SALARIO-ANUAL(1:2)
        3 BONUS(1:1, 1:1)
    2 DIAS-VACACIONES
        3 DIAS-DISPONIBLES         (N02)
        3 DIAS-USADOS              (N02)
    2 LICENCIA-SOLICITADA
        3 FECHA-INICIO(1:1)
        3 FECHA-FIN(1:1)
    2 IDIOMA(1:1)                          /* DESCRIPTOR
    
  1 #REG-ENTRADA
    2 #ID-EMPLEADO   (A08)
    2 #SALARIO       (P09)
    2 #DEPARTAMENTO  (A06)
    2 #PROFESION     (A23)
    2 REDEFINE #PROFESION
      3 #PROFESION-1 (A10)
      3 #PROFESION-2 (A13)
    2 #NOMBRE        (A20)
      2 REDEFINE #NOMBRE
      3 #NOMBRE-1 (A10)
      3 #NOMBRE-2 (A10)
    2 #APELLIDO      (A20)
      2 REDEFINE #APELLIDO
      3 #APELLIDO-1 (A10)
      3 #APELLIDO-2 (A10)
    2 #SEXO          (A01)
    2 #ESTADO-CIVIL  (A01)
    
 1 #TOT              (N02)
 1 #NO-ALTA     (N02)
 1 #ALTA        (N02)
END-DEFINE
*
DEFINE WORK FILE 1 '/home/WF/EMPLEWF3.csv' TYPE 'CSV'

WRITE 'LISTA DE REGISTROS DADOS DE ALTA' /

READ WORK FILE 1 #REG-ENTRADA
*
  ADD 1 TO #TOT
  IF NOT (#ESTADO-CIVIL = 'M' OR = 'S' OR = 'D' OR ='W') THEN
    WRITE 'TIENE ESTADO CIVIL ERRONEO, ID: ' #ID-EMPLEADO
    ADD 1 TO #NO-ALTA
    ESCAPE TOP        /* ANTES ESCAPE BOTTOM
  END-IF
*
  IF NOT (#SEXO = 'M' OR = 'F') THEN
    WRITE 'TIENE SEXO ERRONEO, ID: '        #ID-EMPLEADO
    ADD 1 TO #NO-ALTA /* ANTES ESCAPE BOTTOM
    ESCAPE TOP
  END-IF
*
  FIND EMPL-VISTA WITH EMPL-VISTA.ID-EMPLEADO = #ID-EMPLEADO
    IF NO RECORDS FOUND
*
      MOVE #ID-EMPLEADO  TO EMPL-VISTA.ID-EMPLEADO
      MOVE #SALARIO      TO EMPL-VISTA.SALARIO-ANUAL(2)
      MOVE #DEPARTAMENTO TO EMPL-VISTA.DEPARTAMENTO
      MOVE #PROFESION    TO EMPL-VISTA.PROFESION
      MOVE #NOMBRE       TO EMPL-VISTA.NOMBRE
      MOVE #APELLIDO     TO EMPL-VISTA.APELLIDO
      MOVE #SEXO         TO EMPL-VISTA.SEXO
      MOVE #ESTADO-CIVIL TO EMPL-VISTA.ESTADO-CIVIL
*
      ADD 1 TO #ALTA
      STORE EMPL-VISTA
      END TRANSACTION
      ESCAPE BOTTOM
    END-NOREC
  WRITE 'REGISTRO EXISTENTE, ID: ' EMPL-VISTA.ID-EMPLEADO
  END-FIND
*
END-WORK
*
WRITE / 'REGISTROS TOTALES.........:' #TOT
WRITE   'REGISTROS DADOS DE ALTA...:' #ALTA
WRITE   'REGISTROS NO DADOS DE ALTA:' #NO-ALTA
*
CLOSE WORK FILE 1
END

