* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/** Alta de <b>Empleados</b>.
DEFINE DATA LOCAL
1 EMPL-VISTA VIEW OF EMPLEADOS
    2 ID-EMPLEADO                  (A08)   /* DESCRIPTOR
    2 NOMBRE-COMPLETO
        3 NOMBRE                   (A20)
        3 APELLIDO                 (A20)   /* DESCRIPTOR
        3 NOMBRE-SECUNDARIO        (A20)
*        
    2 ESTADO-CIVIL                 (A01)
    /* M=MARRIED  (CASADO/A)
    /* S=SINGLE   (SOLTERO/A)
    /* D=DIVORCED (DIVORCIADO/A)
    /* W=WIDOWED  (VIUDO/A)
*    
    2 SEXO                         (A01)
    /* M=MALE     (MASCULINO)
    /* F=FEMALE   (FEMENINO)
*    
    2 DOMICILIO-COMPLETO
        3 DOMICILIO(1:1)
        3 CIUDAD                   (A20)   /* DESCRIPTOR
        3 CODIGO-POSTAL            (A10)
        3 INICIAL-PAIS             (A03)
    2 TELEFONO
        3 CODIGO-AREA              (A06)
        3 NUMERO-TELEFONO          (A15)
    2 DEPARTAMENTO                 (A06)   /* DESCRIPTOR
    2 PROFESION                    (A25)   /* DESCRIPTOR
    2 INGRESO-ANUAL
        3 CODIGO-MONEDA(1:1)
        3 SALARIO-ANUAL(1:1)
        3 BONUS(1:1, 1:1)
    2 DIAS-VACACIONES
        3 DIAS-DISPONIBLES         (N02)
        3 DIAS-USADOS              (N02)
    2 LICENCIA-SOLICITADA
        3 FECHA-INICIO(1:1)
        3 FECHA-FIN(1:1)
    2 IDIOMA(1:1)                          /* DESCRIPTOR
*
  1 VISTA-DEPT VIEW OF DEPARTAMENTOS
    2 COD-DEPT
    2 DESC
*
  1 VISTA-MONEDA VIEW OF MONEDAS
    2 COD-MONEDA
    2 DESC
*
/* CAMPOS DEL MAPA
1 #APELLIDO-M                      (A020)
1 #BONUS-M                         (P09.0)
1 #CIUDAD-M                        (A020)
1 #COD-MONEDA-M                    (A003)
1 #DOMICILIO-M                     (A020)
1 #ESTADO-CIVIL-M                  (A001)
1 #FECHA-FIN-M                     (N08.0)
1 REDEFINE #FECHA-FIN-M                    /* FORMATO YYYYMMDD
  2 #FECHA-FIN-M-YYYY              (N04)
  2 #FECHA-FIN-M-MMDD              (N04)
1 #FECHA-INICIO-M                  (N08.0) /* FORMATO YYYYMMDD
1 REDEFINE #FECHA-INICIO-M
  2 #FECHA-INICIO-M-YYYY           (N04)
  2 #FECHA-INICIO-M-MMDD           (N04)
1 #ID-EMPLEADO-M                   (A008)
1 REDEFINE #ID-EMPLEADO-M
  2 #ID-EMPLEADO-M-1               (A01)
  2 #ID-EMPLEADO-M-2               (A07)
1 #NOMBRE-M                        (A020)
1 #SALARIO-ANUAL-M                 (P09.0)
1 #SEXO-M                          (A001)
1 #CODIGO-POSTAL-M                 (A010)
1 #DEPARTAMENTO-M                  (A006)
1 #INICIAL-PAIS-M                  (A003)
1 #PROFESION-M                     (A025)
*
/* VARIABLES PARA CALLNAT PARA EL SUBPROGRAMA 'DEP-SUB1'
  1 #COD-DEPT-P     (A06) /* ESTE ES PARÁMETRO ENTRADA
  1 #DESC-P         (A30) /* ESTE ES PARÁMETRO SALIDA
  1 #COD-RETORNO-P  (N01) /* ESTE ES PARÁMETRO SALIDA                
END-DEFINE
*
DEFINE WINDOW AVISO
  SIZE 7  * 50
  BASE 10 / 30
  FRAMED
*
SET KEY ALL
*
REPEAT
  INPUT USING MAP 'EMP-ALTM'
  DECIDE ON FIRST VALUE OF *PF-KEY
*
    VALUE 'ENTR'
      PERFORM VALIDAR-MAPA
*
    VALUE 'PF3'
      FETCH 'EMP-MENP'
*
    VALUE 'PF10'
      PERFORM VALIDAR-MAPA
      PERFORM ALTA-EMPLEADO
      PERFORM RESETEAR-VARIABLES
*
    NONE VALUE
      IGNORE
  END-DECIDE
END-REPEAT
*
DEFINE SUBROUTINE VALIDAR-MAPA
*
/* VALIDACION DE ID EMPLEADO
  IF #ID-EMPLEADO-M NE MASK(99999999)
    REINPUT 'DEBE INGRESAR UN ID DE 8 DIGITOS' MARK *#ID-EMPLEADO-M
  END-IF
*
  FIND EMPL-VISTA WITH EMPL-VISTA.ID-EMPLEADO =      #ID-EMPLEADO-M
*
    REINPUT 'EL ID EMPLEADO YA EXISTE'         MARK *#ID-EMPLEADO-M
  END-FIND
*
/* VALIDACION DE NOMBRE
  IF #NOMBRE-M EQ ' '
    REINPUT 'INGRESE UN NOMBRE'                MARK *#NOMBRE-M
  END-IF
*
/* VALIDACION DE APELLIDO
  IF #APELLIDO-M EQ ' '
    REINPUT 'INGRESE UN APELLIDO'              MARK *#APELLIDO-M
  END-IF
*
/* VALIDACION DE ESTADO CIVIL
  IF NOT (#ESTADO-CIVIL-M = 'M' OR = 'S' OR = 'D' OR ='W')
*
    REINPUT 'INGRESE UN ESTADO CIVIL VALIDO'   MARK *#ESTADO-CIVIL-M
  END-IF
*
/* VALIDACION DE ESTADO CIVIL
  IF NOT (#SEXO-M = 'M' OR = 'F') THEN
    REINPUT 'INGRESE UN SEXO VALIDO'           MARK *#SEXO-M
  END-IF
*
/* VALIDACION DE DOMICILIO
  IF #DOMICILIO-M EQ ' '
    REINPUT 'INGRESE UN DOMICILIO'             MARK *#DOMICILIO-M
  END-IF
*
/* VALIDACION DE CIUDAD
  IF #CIUDAD-M EQ ' '
    REINPUT 'INGRESE UNA CIUDAD'               MARK *#CIUDAD-M
  END-IF
*
/* VALIDACION DE CODIGO POSTAL
  IF #CODIGO-POSTAL-M EQ ' '
    REINPUT 'INGRESE UN CODIGO POSTAL'         MARK *#CODIGO-POSTAL-M
  END-IF
*
/* VALIDACION DE INICIAL PAIS
*  IF INICIAL-PAIS EQ ' '
*    REINPUT 'INGRESE UN INICIAL PAIS'          MARK *INICIAL-PAIS-M
*  END-IF
*
/* VALIDACION DE DEPARTAMENTO
  IF #DEPARTAMENTO-M EQ ' '
    REINPUT 'INGRESE UN DEPARTAMENTO'          MARK *#DEPARTAMENTO-M
  END-IF
*
*  VERSION VIEJA DE VALIDAR DEPARTAMENTO EXISTENTE
*
*  FIND VISTA-DEPT WITH VISTA-DEPT.COD-DEPT =         #DEPARTAMENTO-M
*    IF NO RECORD FOUND
*      REINPUT 'DEPARTAMENTO INEXISTENTE'       MARK *#DEPARTAMENTO-M
*    END-NOREC
*  END-FIND
*
  MOVE  #DEPARTAMENTO-M TO #COD-DEPT-P
  RESET #DESC-P #COD-RETORNO-P
  CALLNAT 'DEP-SUB1' #COD-DEPT-P #DESC-P #COD-RETORNO-P
*
  IF #COD-RETORNO-P EQ 2
    REINPUT 'DEPARTAMENTO INEXISTENTE'         MARK *#DEPARTAMENTO-M
  END-IF
*
/* VALIDACION DE PROFESION
  IF #PROFESION-M EQ ' '
    REINPUT 'INGRESE UNA PROFESION'            MARK *#PROFESION-M
  END-IF
*
/* VALIDACION DE MONEDA
  IF #COD-MONEDA-M EQ ' '
    REINPUT 'INGRESE UNA MONEDA'               MARK *#COD-MONEDA-M
  END-IF
*
  FIND VISTA-MONEDA WITH VISTA-MONEDA.COD-MONEDA =   #COD-MONEDA-M
    IF NO RECORD FOUND
      REINPUT 'MONEDA INEXISTENTE'             MARK *#COD-MONEDA-M
    END-NOREC
  END-FIND
*
/* VALIDACION DE SALARIO ANUAL
  IF #SALARIO-ANUAL-M NE MASK (999999999) OR #SALARIO-ANUAL-M EQ 0  
    REINPUT 'INGRESE UN SALARIO VÁLIDO'        MARK *#SALARIO-ANUAL-M
  END-IF
*
/* VALIDACION DE BONUS
  IF #BONUS-M NE MASK (999999999) OR #BONUS-M EQ 0
    REINPUT 'INGRESE UN BONUS VÁLIDO'          MARK *#BONUS-M
  END-IF
*
/* VALIDACION DE FECHA INICIO FORMATO MMDD
  IF #FECHA-INICIO-M-MMDD NE MASK(MMDD)
    REINPUT 
    'INGRESE FECHA DE INICIO NO CORRESPONDE AL FORMATO FECHA DIA Y MES' 
                                               MARK 
                                               *#FECHA-INICIO-M-MMDD
  END-IF
*
/* VALIDACION DE FECHA INICIO FORMATO YYYYMMDD
*
  IF #FECHA-INICIO-M NE MASK(YYYYMMDD)
    REINPUT 'INGRESE FECHA DE INICIO NO CORRESPONDE AL FORMATO FECHA' 
                                               MARK *#FECHA-INICIO-M                                           
  END-IF
*
/* VALIDACION DE FECHA FIN FORMATO MMDD
*
  IF #FECHA-FIN-M-MMDD NE MASK(MMDD)
    REINPUT 
    'INGRESE FECHA DE FIN NO CORRESPONDE AL FORMATO FECHA DIA Y MES' 
                                               MARK *#FECHA-FIN-M-MMDD                                        
  END-IF
*
/* VALIDACION DE FECHA FIN FORMATO YYYYMMDD
*
  IF #FECHA-FIN-M NE MASK(YYYYMMDD)
    REINPUT 
    'INGRESE FECHA DE FIN NO CORRESPONDE AL FORMATO FECHA' 
                                               MARK *#FECHA-FIN-M                                              
  END-IF
*
/* VALIDACION DE FECHAS IGUALES
*
  IF #FECHA-FIN-M EQ #FECHA-INICIO-M
    REINPUT 'LAS FECHAS NO DEBEN SER IGUALES'                                       
  END-IF
*
  IF #FECHA-FIN-M LT #FECHA-INICIO-M
    REINPUT 'FECHA DE FIN DEBE SER MAYOR A LA FECHA DE INICIO'                                         
  END-IF
*
END-SUBROUTINE
*
DEFINE SUBROUTINE ALTA-EMPLEADO
*
  RESET EMPL-VISTA
  MOVE #ID-EMPLEADO-M   TO EMPL-VISTA.ID-EMPLEADO
  MOVE #NOMBRE-M        TO EMPL-VISTA.NOMBRE
  MOVE #APELLIDO-M      TO EMPL-VISTA.APELLIDO
  MOVE #ESTADO-CIVIL-M  TO EMPL-VISTA.ESTADO-CIVIL
  MOVE #SEXO-M          TO EMPL-VISTA.SEXO
  MOVE #CIUDAD-M        TO EMPL-VISTA.CIUDAD
  MOVE #CODIGO-POSTAL-M TO EMPL-VISTA.CODIGO-POSTAL
/* SE ASIGNA EL PRIMER DIGITO DEL ID DE EMPLEADO  
  MOVE #ID-EMPLEADO-M-1 TO EMPL-VISTA.INICIAL-PAIS
  MOVE #DEPARTAMENTO-M  TO EMPL-VISTA.DEPARTAMENTO
  MOVE #PROFESION-M     TO EMPL-VISTA.PROFESION
  MOVE #COD-MONEDA-M    TO EMPL-VISTA.CODIGO-MONEDA(1)
  MOVE #SALARIO-ANUAL-M TO EMPL-VISTA.SALARIO-ANUAL(1)
  MOVE #BONUS-M         TO EMPL-VISTA.BONUS        (1,1)
  MOVE #FECHA-INICIO-M  TO EMPL-VISTA.FECHA-INICIO (1)
  MOVE #FECHA-FIN-M     TO EMPL-VISTA.FECHA-FIN    (1)
*
STORE EMPL-VISTA
END TRANSACTION
*
SET WINDOW 'AVISO'
  INPUT 'EL EMPLEADO HA SIDO AGREGADO EXITOSAMENTE'
SET WINDOW OFF
*
END-SUBROUTINE
*
DEFINE SUBROUTINE RESETEAR-VARIABLES
  RESET #ID-EMPLEADO-M
  RESET #NOMBRE-M
  RESET #APELLIDO-M
  RESET #ESTADO-CIVIL-M
  RESET #SEXO-M
  RESET #CIUDAD-M
  RESET #CODIGO-POSTAL-M
  RESET #ID-EMPLEADO-M-1
  RESET #DEPARTAMENTO-M
  RESET #PROFESION-M
  RESET #COD-MONEDA-M
  RESET #SALARIO-ANUAL-M
  RESET #BONUS-M
  RESET #FECHA-INICIO-M
  RESET #FECHA-FIN-M
END-SUBROUTINE
*
END  
