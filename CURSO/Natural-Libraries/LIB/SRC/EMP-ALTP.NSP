* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/** Alta de <b>Empleados</b>.
DEFINE DATA LOCAL
1 EMPL-VISTA VIEW OF EMPLEADOS
    2 ID-EMPLEADO                  (A08) /* DESCRIPTOR
    2 NOMBRE-COMPLETO
        3 NOMBRE                   (A20)
        3 APELLIDO                 (A20) /* DESCRIPTOR
        3 NOMBRE-SECUNDARIO        (A20)
*        
    2 ESTADO-CIVIL                 (A01)
    /* M=MARRIED  (CASADO/A)
    /* S=SINGLE   (SOLTERO/A)
    /* D=DIVORCED (DIVORCIADO/A)
    /* W=WIDOWED  (VIUDO/A)
*    
    2 SEXO                         (A01)
    /* M=MALE     (MASCULINO)
    /* F=FEMALE   (FEMENINO)
*    
    2 DOMICILIO-COMPLETO
        3 DOMICILIO(1:1)
        3 CIUDAD                   (A20) /* DESCRIPTOR
        3 CODIGO-POSTAL            (A10)
        3 INICIAL-PAIS             (A03)
    2 TELEFONO
        3 CODIGO-AREA              (A06)
        3 NUMERO-TELEFONO          (A15)
    2 DEPARTAMENTO                 (A06) /* DESCRIPTOR
    2 PROFESION                    (A25) /* DESCRIPTOR
    2 INGRESO-ANUAL
        3 CODIGO-MONEDA(1:1)
        3 SALARIO-ANUAL(1:1)
        3 BONUS(1:1, 1:1)
    2 DIAS-VACACIONES
        3 DIAS-DISPONIBLES         (N02)
        3 DIAS-USADOS              (N02)
    2 LICENCIA-SOLICITADA
        3 FECHA-INICIO(1:1)
        3 FECHA-FIN(1:1)
    2 IDIOMA(1:1)                        /* DESCRIPTOR
*
  1 VISTA-DEPT VIEW OF DEPARTAMENTOS
    2 COD-DEPT
    2 DESC
*
  1 VISTA-MONEDA VIEW OF MONEDAS
    2 COD-MONEDA
    2 DESC
*
/* CAMPOS DEL MAPA
1 #APELLIDO-M                      (A020)
1 #BONUS-M                         (P09.0)
1 #CIUDAD-M                        (A020)
1 #COD-MONEDA-M                    (A003)
1 #DOMICILIO-M                     (A020)
1 #ESTADO-CIVIL-M                  (A001)
1 #FECHA-FIN                       (N08.0)
1 #FECHA-INICIO-M                  (N08.0)
1 #ID-EMPLEADO-M                   (A008)
1 #NOMBRE-M                        (A020)
1 #SALARIO-ANUAL-M                 (P09.0)
1 #SEXO-M                          (A001)
1 CODIGO-POSTAL-M                  (A010)
1 DEPARTAMENTO-M                   (A006)
1 INICIAL-PAIS-M                   (A003)
1 PROFESION-M                      (A025)                  
END-DEFINE
*
SET KEY ALL
INPUT USING MAP 'EMP-ALTM'
*
DECIDE ON FIRST VALUE OF *PF-KEY
*
  VALUE 'ENTR'
    PERFORM VALIDAR-MAPA
*
  VALUE 'PF3'
    FETCH 'EMP-MENP'
*
  NONE VALUE
    IGNORE
END-DECIDE
*
DEFINE SUBROUTINE VALIDAR-MAPA
*
/* VALIDACION DE ID EMPLEADO
  IF #ID-EMPLEADO-M NE MASK(99999999)
    REINPUT 'DEBE INGRESAR UN ID DE 8 DIGITOS' MARK *#ID-EMPLEADO-M
  END-IF
*
  FIND EMPL-VISTA WITH EMPL-VISTA.ID-EMPLEADO =      #ID-EMPLEADO-M
*
    REINPUT 'EL ID EMPLEADO YA EXISTE'         MARK *#ID-EMPLEADO-M
  END-FIND
*
/* VALIDACION DE NOMBRE
  IF #NOMBRE-M EQ ' '
    REINPUT 'INGRESE UN NOMBRE'                MARK *#NOMBRE-M
  END-IF
*
/* VALIDACION DE APELLIDO
  IF #APELLIDO-M EQ ' '
    REINPUT 'INGRESE UN APELLIDO'              MARK *#APELLIDO-M
  END-IF
*
/* VALIDACION DE ESTADO CIVIL
  IF NOT (#ESTADO-CIVIL-M = 'M' OR = 'S' OR = 'D' OR ='W')
*
    REINPUT 'INGRESE UN ESTADO CIVIL VALIDO'   MARK *#ESTADO-CIVIL-M
  END-IF
*
/* VALIDACION DE ESTADO CIVIL
  IF NOT (#SEXO-M = 'M' OR = 'F') THEN
    REINPUT 'INGRESE UN SEXO VALIDO'           MARK *#SEXO-M
  END-IF
*
/* VALIDACION DE DOMICILIO
  IF #DOMICILIO-M EQ ' '
    REINPUT 'INGRESE UN DOMICILIO'             MARK *#DOMICILIO-M
  END-IF
*
/* VALIDACION DE CIUDAD
  IF #CIUDAD-M EQ ' '
    REINPUT 'INGRESE UNA CIUDAD'               MARK *#CIUDAD-M
  END-IF
*
/* VALIDACION DE CODIGO POSTAL
  IF CODIGO-POSTAL-M EQ ' '
    REINPUT 'INGRESE UN CODIGO POSTAL'         MARK *CODIGO-POSTAL-M
  END-IF
*
/* VALIDACION DE INICIAL PAIS
*  IF INICIAL-PAIS EQ ' '
*    REINPUT 'INGRESE UN INICIAL PAIS'          MARK *INICIAL-PAIS-M
*  END-IF
*
/* VALIDACION DE DEPARTAMENTO
  IF DEPARTAMENTO-M EQ ' '
    REINPUT 'INGRESE UN DEPARTAMENTO'          MARK *DEPARTAMENTO-M
  END-IF
*
  FIND VISTA-DEPT WITH VISTA-DEPT.COD-DEPT =         DEPARTAMENTO-M
    IF NO RECORD FOUND
      REINPUT 'DEPARTAMENTO INEXISTENTE'       MARK *DEPARTAMENTO-M
    END-NOREC
  END-FIND
*
/* VALIDACION DE PROFESION
  IF PROFESION-M EQ ' '
    REINPUT 'INGRESE UNA PROFESION'            MARK *PROFESION-M
  END-IF
*
/* VALIDACION DE MONEDA
  IF #COD-MONEDA-M EQ ' '
    REINPUT 'INGRESE UN DEPARTAMENTO'          MARK *#COD-MONEDA-M
  END-IF
*
  FIND VISTA-MONEDA WITH VISTA-MONEDA.COD-MONEDA =   #COD-MONEDA-M
    IF NO RECORD FOUND
      REINPUT 'MONEDA INEXISTENTE'             MARK *#COD-MONEDA-M
    END-NOREC
  END-FIND
*
END-SUBROUTINE
END  
